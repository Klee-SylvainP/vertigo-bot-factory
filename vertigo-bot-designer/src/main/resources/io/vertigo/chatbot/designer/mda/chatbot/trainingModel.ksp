package io.vertigo.chatbot.commons.domain



/**********************/
/* **** Training **** */
/**********************/
create DtDefinition DtTraining {
	id    traId 		{domain: DoId 			label: "ID"}
	field startTime		{domain: DoInstant 		label: "Start time"			cardinality : "1"}
	field endTime		{domain: DoInstant 		label: "End time" 			}
	field versionNumber	{domain: DoNumber 		label: "Version" 			cardinality : "1"}
	field log			{domain: DoText			label: "Log"	 			}
	field infos			{domain: DoText			label: "Informations"	 	}
	field warnings		{domain: DoText			label: "Warnings"	 		}
	field nluThreshold	{domain: DoPercentage 	label: "NLU Threshold" 		cardinality : "1"}
	computed duration 	{domain: DoLabel 		label: "Duration"
						 expression:"return io.vertigo.chatbot.commons.ChatbotUtils.durationBetween(getStartTime(), getEndTime());" }
}

create Association ATrainingChatbot {
	fkFieldName : "botId"
    
 	dtDefinitionA : DtTraining
	type : "*>1"
	dtDefinitionB : DtChatbot
	
	labelB : "Chatbot"
}

create Association ATrainingMediaFileInfo {
	fkFieldName : "filIdModel"
    
 	dtDefinitionA : DtTraining
	type : "*>?"
	dtDefinitionB : DtMediaFileInfo
	
	
	labelB : "Model"
}

/*******************/
/* **** Nodes **** */
/*******************/
create DtDefinition DtChatbotNode {
	id    nodId 		{domain: DoId 		label: "ID"}
	field name			{domain: DoLabel 	label: "Name" 				cardinality : "1"}
	field url			{domain: DoUrl 		label: "URL" 				cardinality : "1"}
	field isDev			{domain: DoYesNo 	label: "Dev node" 			cardinality : "1"}
	field color			{domain: DoColor 	label: "Color" 				cardinality : "1"}
	field apiKey		{domain: DoLabel 	label: "ApiKey" 			cardinality : "1"}
	field isUpToDate	{domain: DoYesNo 	label: "Is up to date" 		cardinality : "1"}
}

create Association ANodeChatbot {
	fkFieldName : "botId"
    
 	dtDefinitionA : DtChatbotNode
	type : "*>1"
	dtDefinitionB : DtChatbot
	
	labelB : "Chatbot"
}

create Association ANodeTraining{
	fkFieldName : "traId"
    
 	dtDefinitionA : DtChatbotNode
	type : "*>?"
	dtDefinitionB : DtTraining
	
	labelB : "Loaded model"
}


/************************/
/* ****    TRAINING STATUS    **** */
/************************/

create DtDefinition DtTrainingStatus {
	stereotype : "StaticMasterData"
	id    strCd 		{domain: DoCode 	label: "ID"}
	field label 		{domain: DoLabel 	label: "Label" 			cardinality : "1"	}
	field labelFr 		{domain: DoLabel 	label: "LabelFr" 		cardinality : "1"	}
	values : `{
        "OK" : {
            "strCd" : "OK",
            "label" : "OK",
            "labelFr" : "OK"           
        },
        "KO" : {
            "strCd" : "KO",
            "label" : "KO",
            "labelFr" : "KO"           
        },
        "TRAINING" : {
            "strCd" : "TRAINING",
            "label" : "TRAINING",
            "labelFr" : "En cours"           
        }
     }`
    sortField : "strCd"
 	displayField : "label"
}


create Association ATrainingTrainingStatus{
	fkFieldName : "strCd"
    
 	dtDefinitionA : DtTraining
	type : "*>1"
	dtDefinitionB : DtTrainingStatus
	
	labelB : "Status"
}
